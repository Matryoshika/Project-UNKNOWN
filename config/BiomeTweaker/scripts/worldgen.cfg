#Replace all biomes with Highlands
	Tweaker.setStage("FINISHED_LOAD")
	
#Assign groups
	highland_mountain_replacees = forBiomes("minecraft:forest", "minecraft:forest_hills")
	highland_mountain           = forBiomes("Biometweaker:highlands_mountain")
	flats_replacees             = forBiomes("minecraft:birch_forest", "minecraft:birch_forest_hills", "minecraft:mutated_birch_forest_hills", "minecraft:mutated_birch_forest")
	flats                       = forBiomes("biometweaker:flats")
	river_replacees             = forBiomes("minecraft:river", "minecraft:beaches", "minecraft:cold_beach", "minecraft:frozen_river")
	river                       = forBiomes("biometweaker:river")
	lake_replacees              = forBiomes("minecraft:extreme_hills", "minecraft:smaller_extreme_hills")
	lake                        = forBiomes("biometweaker:lake")
	dont_touch                  = forBiomes("minecraft:hell", "minecraft:sky", "minecraft:void")
	highlands                   = forBiomes("biometweaker:highlands")
	everything_else             = forAllBiomesExcept(highland_mountain_replacees, highland_mountain, flats_replacees, flats, river_replacees, river, lake_replacees, lake, dont_touch)
	overworld                   = forBiomes(highland_mountain, flats, river, lake, highlands)
	
#Replace biomes with custom ones
	highland_mountain_replacees.registerGenBiomeRep(highland_mountain)
	flats_replacees.registerGenBiomeRep(flats)
	river_replacees.registerGenBiomeRep(river)
	lake_replacees.registerGenBiomeRep(lake)
	everything_else.registerGenBiomeRep(highlands)
	
#Make biomes smaller, for a bit more diversity and make flats not as ugly
	Tweaker.setAverageBiomeSize("default", 2)

#Misc tweaks
	overworld.set("enableRain",true)
	
	
	
	
#Block-specific tweaks
	Tweaker.setPlacementStage("POST_ORES")
	Tweaker.setWorld(0)
#Fix village spawns
	flats.set("genVillages", false)
	river.set("genVillages", false)
	lake.set("genVillages", false)
	highland_mountain.set("genVillages", true)
	highlands.set("genVillages", true)
	
#Remove Strongholds and other features
	overworld.set("genStrongholds", false)
	overworld.set("genScatteredFeatures", false)
	overworld.removeFeature("LAVA")
	overworld.removeFeature("NETHER_LAVA")
	overworld.removeFeature("LAKE")
	overworld.removeFeature("DUNGEON")
	overworld.removeDecoration("LAKE_WATER")
	overworld.removeDecoration("LAKE_LAVA")
	overworld.removeDecoration("SAND")
	overworld.removeDecoration("CLAY")
	overworld.removeDecoration("GRAVEL")
	
#Remove ores; Realistic Ore Veins will handle this instead
	overworld.removeOre("COAL")
	overworld.removeOre("DIAMOND")
	overworld.removeOre("DIRT")
	overworld.removeOre("GOLD")
	overworld.removeOre("GRAVEL")
	overworld.removeOre("IRON")
	overworld.removeOre("LAPIS")
	overworld.removeOre("REDSTONE")
	overworld.removeOre("QUARTZ")
	overworld.removeOre("DIORITE")
	overworld.removeOre("GRANITE")
	overworld.removeOre("ANDESITE")
	overworld.removeOre("EMERALD")
	overworld.removeOre("SILVERFISH")
	overworld.removeOre("CUSTOM")
	
	

	